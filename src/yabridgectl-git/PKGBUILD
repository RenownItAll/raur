# Maintainer: Renowned <64113367+RenownItAll@users.noreply.github.com>
pkgname=yabridgectl-git
_pkgname=yabridgectl
_yabridge=yabridge
pkgver=5.1.1.r20.ga69072ef
pkgrel=3
pkgdesc="Command-line utility to configure and manage yabridge"
arch=('x86_64')
url="https://github.com/robbert-vdh/yabridge"
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'cargo')
provides=('yabridgectl')
conflicts=('yabridgectl' 'yabridge-bin')
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_yabridge"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_yabridge/tools/yabridgectl"
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$_yabridge/tools/yabridgectl"
  export CARGO_TARGET_DIR=target
  cargo build --release --offline --all-features
}

package() {
  cd "$srcdir/$_yabridge/tools/yabridgectl"
  install -Dm755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
}
