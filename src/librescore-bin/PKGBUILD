# Maintainer: Renowned <64113367+RenownItAll@users.noreply.github.com>
pkgname=librescore-bin
pkgver=6.0.21
pkgrel=5
pkgdesc="Download sheet music from MuseScore.com for free (AppImage wrapper)"
arch=('x86_64')
url="https://github.com/LibreScore/app-librescore"
license=('GPL-3.0-or-later')
depends=('alsa-lib' 'nss' 'gtk3' 'glibc' 'zlib' 'hicolor-icon-theme')
makedepends=('librsvg')
options=('!strip' '!buildflags')

source=("${pkgname}-${pkgver}.AppImage::${url}/releases/download/v${pkgver}/LibreScore.AppImage")
sha256sums=('a192bda328e8ab1f3284c02c1021d595becca5b40fd654da8d44d5188d32e98b')

prepare() {
  chmod +x "${pkgname}-${pkgver}.AppImage"
  ./${pkgname}-${pkgver}.AppImage --appimage-extract >/dev/null
}

package() {
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}.AppImage" "${pkgdir}/usr/bin/librescore"

  cd "${srcdir}/squashfs-root"

  sed -i 's|^Exec=.*|Exec=/usr/bin/librescore %U|' share/applications/librescore.desktop
  install -Dm644 share/applications/librescore.desktop "${pkgdir}/usr/share/applications/librescore.desktop"

  local _svg_src="share/icons/hicolor/scalable/apps/librescore.svg"
  install -Dm644 "$_svg_src" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/librescore.svg"

  for size in 16 24 32 48 64 96 128 256 512; do
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps"
    rsvg-convert -w "$size" -h "$size" "$_svg_src" -o "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/librescore.png"
  done

  find "${pkgdir}/usr/share/icons" -type d -exec chmod 755 {} +
  find "${pkgdir}/usr/share/icons" -type f -exec chmod 644 {} +
}
